apply plugin: 'com.android.library'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                targets "svgren"
            }
        }
    }

    // This is needed to make the default dependency configuration to be 'debug' for the test app which references
    // this module as in-project dependency. Because app searches for library to link against in debug directory.
    defaultPublishConfig "debug"

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Encapsulates your external native build configurations.
    externalNativeBuild {

        // Encapsulates your CMake build configurations.
        cmake {
            // Provides a relative path to your CMake build script.
            path "CMakeLists.txt"
        }
    }
}

repositories {
    maven {
        url 'https://dl.bintray.com/igagis/android/'
    }
}
dependencies {
    implementation 'io.github.igagis:utki:+'
    implementation 'io.github.igagis:svgdom:+'
    implementation 'org.cairographics:cairo:+'
}


//===================================
//=== Extract NDK files from AARs ===

// This is needed to be able to write "configurations.implementation.each" below.
configurations.implementation.setCanBeResolved(true)

task extractNDKLibs {
    doLast {
        configurations.implementation.each {
            def file = it.absoluteFile
            copy {
                from zipTree(file)
                into "build/"
                include "ndkLibs/**/*"
            }
        }
    }
}

tasks.whenTaskAdded { task ->
    logger.info('task added')
    if (task.name.startsWith('externalNativeBuild')) {
        task.dependsOn extractNDKLibs
    }
}



//=================================
//=== pack library files to aar ===
//This stuff re-packs the release AAR file adding headers and static libs to there, but removing all shared (.so) libs, as we don't need them. The resulting AAR is put to the project root directory and can be uploaded to Maven along with POM file (you need to write one by hand).

def aarName = name

task copyNdkLibsToAAR(type: Zip) {
    baseName = aarName
    version = "\$(version)"
    extension = 'aar.in'
    destinationDir = file('..') //put resulting AAR file to upper level directory

    from zipTree("build/outputs/aar/" + aarName + "-release.aar")
    exclude('**/*.so') //do not include shared libraries into final AAR
    from("../../src") {
        include('**/*.hpp')
        include('**/*.h')
        into('ndkLibs/include')
    }
    from("build/intermediates/cmake/debug/obj"){
        include('**/*.a')
        into('ndkLibs')
    }
}

afterEvaluate {
    bundleReleaseAar.finalizedBy copyNdkLibsToAAR
}
